import win.ui;
import web.msxml;
import systool;
import log;
/*DSG{{*/
var winform = win.form(text="新增系统命令";right=540;bottom=194;border="dialog frame";max=false;min=false)
winform.add(
btnSure={cls="plus";text="确定";left=415;top=76;right=505;bottom=110;bgcolor=11580047;db=1;dr=1;font=LOGFONT(h=-16;name='FontAwesome');notify=1;tabstop=1;z=6};
cmdId={cls="edit";left=87;top=16;right=177;bottom=46;edge=1;num=1;z=4};
cmdName={cls="edit";left=87;top=74;right=403;bottom=109;edge=1;z=5};
groupbox={cls="groupbox";text="来源：";left=205;top=9;right=403;bottom=63;edge=1;z=1};
radioClient={cls="radiobutton";text="前端";left=253;top=29;right=312;bottom=58;z=8};
radioServer={cls="radiobutton";text="后端";left=322;top=29;right=381;bottom=58;z=9};
static={cls="static";text="命令id：";left=20;top=22;right=77;bottom=46;transparent=1;z=2};
static2={cls="static";text="命令名称：";left=8;top=73;right=77;bottom=99;transparent=1;z=3};
tipInfo={cls="static";text="Static";left=88;top=124;right=399;bottom=160;color=255;transparent=1;z=7}
)
/*}}*/

winform.radioClient.checked = true;
var tipInfo = winform.tipInfo;
tipInfo.text = "";

winform.btnSure.oncommand = function(id,event){
	tipInfo.text = "";
	var sysId = systool.GetNowSysId();
	var cmdId = tonumber(winform.cmdId.text);
	if not cmdId and cmdId != 0{
		tipInfo.text = "命令id不能为空";
		return; 
	}
	if sysId != 1111 and (cmdId < 0 or cmdId > 255){
		tipInfo.text = "命令id范围是0到255";
		return; 
	}
	
	var cmdName = winform.cmdName.text;
	if string.len(cmdName) < 1 {
		tipInfo.text = "命令名称不能为空";
		return; 
	}
	
	var from = 1;
	var fromText = winform.radioClient.text;
	if winform.radioServer.checked {
		from = 2;
		fromText = winform.radioServer.text;
	}
	
	var filePath = string.format("/protocol/%s.xml", sysId);
	//LOG_DEBUG("filePath", filePath);
	var xmlDoc = web.msxml();
	xmlDoc.load(filePath);
	var root = xmlDoc.documentElement;
	var protocolsNode = root.selectSingleNode("/protocols");
	var cmdNodeList = protocolsNode.selectNodes("cmd");
	for index = 0; cmdNodeList.length - 1; 1 {
		var cmdNode = cmdNodeList(index);
		var cmdId2 = tonumber(cmdNode.getAttribute("id"));
		var from2 = tonumber(cmdNode.getAttribute("from"));
		//LOG_DEBUG("cmdId2", cmdId2, "from2", from2);
		if cmdId == cmdId2 and from == from2 {
			tipInfo.text = "该命令已经存在了！来源：" + fromText;
			return; 
		}
	}
	
	var cmdNode = xmlDoc.createNode("element", "cmd", "");
	cmdNode.text = cmdName;
	cmdNode.setAttribute("id", cmdId);
	cmdNode.setAttribute("from", from);
	cmdNode.setAttribute("name", cmdName);
	protocolsNode.appendChild(cmdNode);
	
	//<cmd id="1" from="1" name="test">111</cmd></protocols>
	var xmlstr = string.replace(xmlDoc.xml,"@</cmd></protocols>",'</cmd>\n</protocols>'); //禁用模式匹配替换
	var xmlDoc = web.msxml(xmlstr);
	xmlDoc.save(filePath);
	//tipInfo.text = "命令添加成功!";
	winform.refreshSysId = true;
	winform.close();
}

winform.show();
win.loopMessage();
return winform;